apply plugin: 'maven-publish'

version            = '13.1'
group              = 'org.signal'
archivesBaseName   = 'argon2'

ext {
    commitTag = "git describe --tags --exact-match".execute().text.trim() ?: "untagged"
}

publishing {
    repositories {
        mavenLocal()
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/mollyim/argon2')
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        argon2(MavenPublication) {
            groupId = 'im.molly'
            artifactId = archivesBaseName
            version = project.hasProperty('ci') ? commitTag : this.version

            artifact("$buildDir/outputs/aar/${archivesBaseName}-release.aar")

            pom {
                name = archivesBaseName
                description = 'Argon2 Android library wrapping https://github.com/P-H-C/phc-winner-argon2'
                url = 'https://github.com/mollyim/argon2'

                scm {
                    url = 'scm:git@github.com:mollyim/argon2.git'
                    connection = 'scm:git@github.com:mollyim/argon2.git'
                    developerConnection = 'scm:git@github.com:mollyim/argon2.git'
                }

                licenses {
                    license {
                        name = 'GPLv3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}
